apply plugin: 'com.android.application'

def getVersionName = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags', '--dirty'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }
    catch (ignored) {
        return null;
    }
}

android {
    signingConfigs {
        release {
            keyAlias 'android'
            keyPassword System.getenv("KEYPWD")
            storeFile file(System.getenv("HOME") + "/.keystore-android.jks")
            storePassword System.getenv("KSTOREPWD")
        }
    }
    compileSdkVersion 23
    buildToolsVersion '26.0.2'
    defaultConfig {
        applicationId "com.pbrane.mike.lifeclock"
        minSdkVersion 23
        targetSdkVersion 23
        versionName getVersionName()
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            applicationVariants.all { variant ->
                variant.outputs.all { output ->
                    def vstr = variant.versionName.toString().split("-")
                    def appName = "lifeclock-" + vstr[0] + "-" + vstr[1] + ".apk"
                    outputFileName = new File(appName)
                }
            }
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:appcompat-v7:23.4.0'
    compile 'joda-time:joda-time:2.9.6'
    compile 'org.joda:joda-convert:1.8.1'
    compile 'com.google.code.google-collections:google-collect:snapshot-20080530'
}
